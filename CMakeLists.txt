CMAKE_MINIMUM_REQUIRED (VERSION 2.8)

# Disable in source build
IF(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR})
    MESSAGE(FATAL_ERROR "In-source builds not allowed. Please make a new directory and run CMake from there.")
ENDIF(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR})

PROJECT (vSMC C CXX)

# User configurations
IF (EXISTS ${PROJECT_SOURCE_DIR}/user_options.cmake)
    INCLUDE (${PROJECT_SOURCE_DIR}/user_options.cmake)
ENDIF (EXISTS ${PROJECT_SOURCE_DIR}/user_options.cmake)

# Configuration files
FILE (READ ${PROJECT_SOURCE_DIR}/README.md README)
SET (DOC_SOURCE  ${PROJECT_SOURCE_DIR}/doc)
SET (DOC_INCLUDE ${PROJECT_SOURCE_DIR}/include)
CONFIGURE_FILE (
    ${PROJECT_SOURCE_DIR}/config/Doxyfile.in
    ${PROJECT_BINARY_DIR}/Doxyfile)
CONFIGURE_FILE (
    ${PROJECT_SOURCE_DIR}/doc/main.md.in
    ${PROJECT_SOURCE_DIR}/doc/main.md)

# Options
OPTION (VSMC_DISABLE_BUILD   "Disable building all")
OPTION (VSMC_DISABLE_TEST    "Disable building tests")
OPTION (VSMC_DISABLE_EXAMPLE "Disable building examples")
OPTION (VSMC_TRY_FUTURE      "Try C++11 <future>")

IF (NOT VSMC_INSTALL_INC_DIR)
    SET (VSMC_INSTALL_INC_DIR include)
ENDIF (NOT VSMC_INSTALL_INC_DIR)

# Installation
INSTALL (FILES include/vsmc.hpp DESTINATION ${VSMC_INSTALL_INC_DIR})
INSTALL (FILES include/vsmc_rcpp.hpp DESTINATION ${VSMC_INSTALL_INC_DIR})
INSTALL (DIRECTORY include/vsmc/ DESTINATION ${VSMC_INSTALL_INC_DIR}/vsmc
    PATTERN ".DS_Store" EXCLUDE PATTERN "*.swp" EXCLUDE)

# Documentations
INCLUDE (FindDoxygen)
FILE (GLOB_RECURSE DOC_SOURCE_FILES      ${PROJECT_SOURCE_DIR}/doc/*.*)
FILE (GLOB_RECURSE DOC_INCLUDE_FILES_C   ${PROJECT_SOURCE_DIR}/include/*.h)
FILE (GLOB_RECURSE DOC_INCLUDE_FILES_CPP ${PROJECT_SOURCE_DIR}/include/*.hpp)
IF (DOXYGEN_FOUND)
    ADD_CUSTOM_COMMAND (
        OUTPUT  ${PROJECT_BINARY_DIR}/doc
        DEPENDS ${PROJECT_BINARY_DIR}/Doxyfile
        ${DOC_SOURCE_FILES} ${DOC_INCLUDE_FILES_C} ${DOC_INCLUDE_FILES_CPP}
        WORKING_DIRECTORY ${PROJECT_BINARY_DIR}
        COMMAND ${DOXYGEN_EXECUTABLE} ARGS Doxyfile)
    ADD_CUSTOM_TARGET(docs DEPENDS ${PROJECT_BINARY_DIR}/doc)
ENDIF (DOXYGEN_FOUND)

# Finish here if we disable build
IF (VSMC_DISABLE_TEST AND VSMC_DISABLE_EXAMPLE)
    SET (VSMC_DISABLE_BUILD ON)
ENDIF (VSMC_DISABLE_TEST AND VSMC_DISABLE_EXAMPLE)
IF (VSMC_DISABLE_BUILD)
    RETURN ()
ENDIF (VSMC_DISABLE_BUILD)

# Module path
SET (CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${PROJECT_SOURCE_DIR}/cmake)

# Required Includes
SET (CMAKE_REQUIRED_INCLUDES ${CMAKE_REQUIRED_INCLUDES}
    ${PROJECT_SOURCE_DIR}/include)

# Includes
INCLUDE_DIRECTORIES (${PROJECT_SOURCE_DIR}/include)
SET (VSMC_INCLUDE_DIR ${PROJECT_SOURCE_DIR}/include)

# Boost
SET (Boost_NO_BOOST_CMAKE ON)
SET (Boost_ADDITIONAL_VERSIONS
    "1.49" "1.49.0"
    "1.50" "1.50.0"
    "1.51" "1.51.0"
    "1.52" "1.52.0"
    "1.53" "1.53.0"
    "1.54" "1.54.0")
IF (NOT Boost_FOUND)
    INCLUDE (FindBoost)
ENDIF (NOT Boost_FOUND)
IF (Boost_FOUND)
    INCLUDE_DIRECTORIES (SYSTEM ${Boost_INCLUDE_DIRS})
    SET (CMAKE_REQUIRED_INCLUDES ${CMAKE_REQUIRED_INCLUDES}
        ${Boost_INCLUDE_DIRS})
ENDIF (Boost_FOUND)

# Special math functions flags
INCLUDE (FindCXXMathIsfinite)
IF (CXX_MATH_ISFINITE_C99_FOUND)
    ADD_DEFINITIONS (-DVSMC_USE_C99_ISFINITE)
ELSEIF (CXX_MATH_ISFINITE_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_USE_STD_ISFINITE)
ELSEIF (CXX_MATH_ISFINITE_BOOST_FOUND)
    ADD_DEFINITIONS (-DVSMC_USE_BOOST_ISFINITE)
ENDIF (CXX_MATH_ISFINITE_C99_FOUND)

INCLUDE (FindCXXMathLgamma)
IF (CXX_MATH_LGAMMA_C99_FOUND)
    ADD_DEFINITIONS (-DVSMC_USE_C99_LGAMMA)
ELSEIF (CXX_MATH_LGAMMA_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_USE_STD_LGAMMA)
ELSEIF (CXX_MATH_LGAMMA_BOOST_FOUND)
    ADD_DEFINITIONS (-DVSMC_USE_BOOST_LGAMMA)
ENDIF (CXX_MATH_LGAMMA_C99_FOUND)

# C++11 libs flags
INCLUDE (vSMCFindFunctional)
IF (VSMC_FUNCTIONAL_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_FUNCTIONAL=1)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_CXX11LIB_FUNCTIONAL=1)
ELSE (VSMC_FUNCTIONAL_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_FUNCTIONAL=0)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_CXX11LIB_FUNCTIONAL=0)
ENDIF (VSMC_FUNCTIONAL_STD_FOUND)

INCLUDE (vSMCFindRandom)
IF (VSMC_RANDOM_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_RANDOM=1)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_CXX11LIB_RANDOM=1)
ELSE (VSMC_RANDOM_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_RANDOM=0)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_CXX11LIB_RANDOM=0)
ENDIF (VSMC_RANDOM_STD_FOUND)
IF (VSMC_RANDOM123_FOUND)
    INCLUDE_DIRECTORIES (${Random123_INCLUDE_DIR})
    ADD_DEFINITIONS (-DVSMC_USE_RANDOM123=1)
ELSE (VSMC_RANDOM123_FOUND)
    ADD_DEFINITIONS (-DVSMC_USE_RANDOM123=0)
ENDIF (VSMC_RANDOM123_FOUND)

INCLUDE (vSMCFindChrono)
IF (VSMC_CHRONO_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_CHRONO=1)
ELSE (VSMC_CHRONO_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_CHRONO=0)
ENDIF (VSMC_CHRONO_FOUND)

INCLUDE (vSMCFindSTDTBB)
IF (VSMC_STDTBB_FOUND)
    SET (VSMC_USE_STD TRUE)
    IF (VSMC_FUTURE_FOUND)
        ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_FUTURE=1)
    ELSE (VSMC_FUTURE_FOUND)
        ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_FUTURE=0)
    ENDIF (VSMC_FUTURE_FOUND)
ELSE (VSMC_STDTBB_FOUND)
    SET (VSMC_USE_STD FALSE)
ENDIF (VSMC_STDTBB_FOUND)

INCLUDE (vSMCFindTuple)

# Third-patry features

SET (VSMC_USE_SEQ TRUE)

IF (${CMAKE_CXX_COMPILER_ID} MATCHES "Intel")
    INCLUDE (FindCilk)
ENDIF (${CMAKE_CXX_COMPILER_ID} MATCHES "Intel")
IF (CILK_FOUND)
    SET (VSMC_USE_CILK TRUE)
ELSE (CILK_FOUND)
    SET (VSMC_USE_CILK FALSE)
ENDIF (CILK_FOUND)

INCLUDE (vSMCFindGCD)
IF (VSMC_GCD_FOUND)
    INCLUDE_DIRECTORIES (SYSTEM ${GCD_INCLUDE_DIR})
    SET (VSMC_USE_GCD TRUE)
ELSE (VSMC_GCD_FOUND)
    SET (VSMC_USE_GCD FALSE)
ENDIF (VSMC_GCD_FOUND)

IF (NOT ${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
    INCLUDE (vSMCFindOpenMP)
ENDIF (NOT ${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
IF (VSMC_OPENMP_FOUND)
    SET (VSMC_USE_OMP TRUE)
ELSE (VSMC_OPENMP_FOUND)
    SET (VSMC_USE_OMP FALSE)
ENDIF (VSMC_OPENMP_FOUND)

IF (MSVC_VERSION GREATER 1599)
    MESSAGE (STATUS "Found Microsoft PPL support")
    SET (VSMC_PPL_FOUND TRUE CACHE BOOL "Found PPL")
ELSE (MSVC_VERSION GREATER 1599)
    SET (VSMC_PPL_FOUND FALSE CACHE BOOL "NOT Found PPL")
ENDIF (MSVC_VERSION GREATER 1599)
IF (VSMC_PPL_FOUND)
    SET (VSMC_USE_PPL TRUE)
ELSE (VSMC_PPL_FOUND)
    SET (VSMC_USE_PPL FALSE)
ENDIF (VSMC_PPL_FOUND)

INCLUDE (FindTBB)
IF (TBB_FOUND)
    INCLUDE_DIRECTORIES (SYSTEM ${TBB_INCLUDE_DIR})
    SET (VSMC_USE_TBB TRUE)
ELSE (TBB_FOUND)
    SET (VSMC_USE_TBB FALSE)
ENDIF (TBB_FOUND)

INCLUDE (vSMCFindOpenCL)
IF (VSMC_OPENCL_FOUND)
    INCLUDE_DIRECTORIES (SYSTEM ${OpenCL_INCLUDE_DIR})
    SET (VSMC_CL_INCLUDE_DIR ${PROJECT_SOURCE_DIR}/include)
ENDIF (VSMC_OPENCL_FOUND)

IF (VSMC_BUILD_MPI)
    INCLUDE (vSMCFindMPI)
ENDIF (VSMC_BUILD_MPI)
IF (VSMC_MPI_FOUND)
    INCLUDE_DIRECTORIES (SYSTEM ${MPI_CXX_INCLUDE_PATH})
ENDIF (VSMC_MPI_FOUND)

# AMD libm
IF (UNIX AND NOT APPLE)
    IF (${CMAKE_CXX_COMPILER_ID} MATCHES "GNU" OR
            ${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
        FIND_LIBRARY (AMDLIBM_LINK_LIBRARY NAMES libamdlibm.a amdlib)
    ENDIF (${CMAKE_CXX_COMPILER_ID} MATCHES "GNU" OR
        ${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
    IF (AMDLIBM_LINK_LIBRARY)
        MESSAGE (STATUS "Use AMD math library: ${AMDLIBM_LINK_LIBRARY}")
    ELSE (AMDLIBM_LINK_LIBRARY)
        UNSET (AMDLIBM_LINK_LIBRARY CACHE)
    ENDIF (AMDLIBM_LINK_LIBRARY)
ENDIF (UNIX AND NOT APPLE)

# Linux librt
IF (UNIX AND NOT APPLE)
    FIND_LIBRARY (LINUX_LIBRT rt)
ENDIF (UNIX AND NOT APPLE)

# Make sure __STDC_CONSTANT_MACROS is defined
ADD_DEFINITIONS (-D__STDC_CONSTANT_MACROS)

# Workaround for vanilla GCC on Mac OS X
IF (APPLE)
    ADD_DEFINITIONS (-U__OBJC__)
    ADD_DEFINITIONS (-U__OBJC2__)
ENDIF (APPLE)

# Disable MSVC iterator warning. Callers are responsible for checking bounds!
IF (MSVC)
    ADD_DEFINITIONS (-D_SCL_SECURE_NO_WARNINGS)
ENDIF (MSVC)

IF (VSMC_FUNCTIONAL_FOUND AND VSMC_RANDOM_FOUND)
    INCLUDE (vSMCFunctions)
    ENABLE_TESTING ()
    ADD_CUSTOM_TARGET (buildall)
    SET (GENERATED_SOURCE CACHE INTERNAL "Generated source")
    IF (NOT VSMC_DISABLE_TEST)
        ADD_SUBDIRECTORY (test EXCLUDE_FROM_ALL)
    ENDIF (NOT VSMC_DISABLE_TEST)
    IF (NOT VSMC_DISABLE_EXAMPLE)
        ADD_SUBDIRECTORY (example EXCLUDE_FROM_ALL)
    ENDIF (NOT VSMC_DISABLE_EXAMPLE)
    ADD_CUSTOM_TARGET (clean-generated)
    ADD_CUSTOM_COMMAND (TARGET clean-generated
        COMMAND ${CMAKE_COMMAND} ARGS -E remove ${GENERATED_SOURCE})
ENDIF (VSMC_FUNCTIONAL_FOUND AND VSMC_RANDOM_FOUND)
