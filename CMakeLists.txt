CMAKE_MINIMUM_REQUIRED (VERSION 2.8)

# disable in source build
IF(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR})
    MESSAGE(FATAL_ERROR "In-source builds not allowed. Please make a new directory and run CMake from there.")
ENDIF(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR})

PROJECT (vSMC C CXX)

# Version information
SET (VERSION_MAJOR 0)
SET (VERSION_MINOR 9)
SET (VERSION_PATCH 0)

# Global path configurations
SET (CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${PROJECT_SOURCE_DIR}/cmake)

# Finding Packages
IF (WIN32)
    IF (NOT BOOST_ROOT)
        SET (BOOST_ROOT "C:/Program Files/Boost")
    ENDIF (NOT BOOST_ROOT)

    IF (NOT Eigen_INC_PATH)
        SET (Eigen_INC_PATH "C:/Program Files/Eigen")
    ENDIF (NOT Eigen_INC_PATH)

    IF (NOT Random123_INC_PATH)
        SET (Random123_INC_PATH "C:/Program Files/Random123/include")
    ENDIF (NOT Random123_INC_PATH)

    IF (MSVC)
        IF ($ENV{TBBROOT})
            FILE (TO_CMAKE_PATH $ENV{TBBROOT} TBB_ROOT)
        ELSE ($ENV{TBBROOT})
            SET (TBB_ROOT "C:/Program Files/TBB")
        ENDIF ($ENV{TBBROOT})

        IF (NOT TBB_INC_PATH)
            SET (TBB_INC_PATH "${TBB_ROOT}/include")
        ENDIF (NOT TBB_INC_PATH)

        IF (NOT TBB_LIB_PATH)
            IF (CMAKE_CL_64)
                SET (TBB_MSVC_LIB "intel64")
            ELSE (CMAKE_CL_64)
                SET (TBB_MSVC_LIB "ia32")
            ENDIF (CMAKE_CL_64)
            IF (MSVC_VERSION GREATER 1599)
                SET (TBB_MSVC_LIB "${TBB_MSVC_LIB}/vc10")
            ELSEIF (MSVC_VERSION GREATER 1499)
                SET (TBB_MSVC_LIB "${TBB_MSVC_LIB}/vc9")
            ELSEIF (MSVC_VERSION GREATER 1399)
                SET (TBB_MSVC_LIB "${TBB_MSVC_LIB}/vc8")
            ENDIF (MSVC_VERSION GREATER 1599)
            SET (TBB_LIB_PATH "${TBB_ROOT}/lib/${TBB_MSVC_LIB}")
            SET (TBB_DLL_PATH "${TBB_ROOT}/bin/${TBB_MSVC_LIB}")
        ENDIF (NOT TBB_LIB_PATH)
    ENDIF (MSVC)
ENDIF (WIN32)

SET (Boost_NO_BOOST_CMAKE     ON)
SET (Boost_USE_STATIC_LIBS    ON)
SET (Boost_USE_MULTITHREADED  ON)
SET (Boost_USE_STATIC_RUNTIME OFF)
INCLUDE (FindBoost)
INCLUDE (FindDoxygen)
INCLUDE (FindEigen)
INCLUDE (FindOpenCL)
INCLUDE (FindRandom123)
INCLUDE (FindTBB)

# Configuration files generation
CONFIGURE_FILE (
    ${PROJECT_SOURCE_DIR}/config/version.hpp.in
    ${PROJECT_SOURCE_DIR}/include/vsmc/internal/version.hpp)

# Installation
IF (NOT INSTALL_INCLUDE_DIR)
    SET (INSTALL_INCLUDE_DIR include)
ENDIF (NOT INSTALL_INCLUDE_DIR)
INSTALL (FILES     include/vsmc.hpp DESTINATION ${INSTALL_INCLUDE_DIR})
INSTALL (DIRECTORY include/vsmc/    DESTINATION ${INSTALL_INCLUDE_DIR}/vsmc
    PATTERN ".DS_Store" EXCLUDE
    PATTERN "*.swp"     EXCLUDE)

# Documentations
IF (DOXYGEN_FOUND)
    CONFIGURE_FILE (
        ${PROJECT_SOURCE_DIR}/config/Doxyfile.in
        ${PROJECT_BINARY_DIR}/Doxyfile)
    FILE (GLOB_RECURSE INCLUDE_SRC
        ${PROJECT_SOURCE_DIR}/include/*.hpp)
    ADD_CUSTOM_COMMAND (
        OUTPUT  ${PROJECT_BINARY_DIR}/include
        DEPENDS ${INCLUDE_SRC}
        COMMAND ${CMAKE_COMMAND}
        ARGS -E copy_directory
        ${PROJECT_SOURCE_DIR}/include
        ${PROJECT_BINARY_DIR}/include)
    FILE (READ ${PROJECT_SOURCE_DIR}/README.markdown README)
    FILE (WRITE ${PROJECT_SOURCE_DIR}/doc/main.markdown
        "vSMC {#mainpage}\n================\n\n${README}\n\n")
    FILE (GLOB_RECURSE DOC_SRC ${PROJECT_SOURCE_DIR}/doc
        *.markdown *.html *.css)
    ADD_CUSTOM_COMMAND (
        OUTPUT  ${PROJECT_BINARY_DIR}/doc_src
        DEPENDS ${DOC_SRC}
        COMMAND ${CMAKE_COMMAND}
        ARGS -E copy_directory
        ${PROJECT_SOURCE_DIR}/doc
        ${PROJECT_BINARY_DIR}/doc_src)
    ADD_CUSTOM_COMMAND (
        OUTPUT  ${PROJECT_BINARY_DIR}/doc
        DEPENDS ${PROJECT_BINARY_DIR}/Doxyfile
        ${PROJECT_BINARY_DIR}/include
        ${PROJECT_BINARY_DIR}/doc_src
        WORKING_DIRECTORY ${PROJECT_BINARY_DIR}
        COMMAND ${DOXYGEN_EXECUTABLE} ARGS Doxyfile)
    ADD_CUSTOM_TARGET(docs DEPENDS ${PROJECT_BINARY_DIR}/doc)
ENDIF (DOXYGEN_FOUND)

# Testing
INCLUDE (CheckCXXSourceRuns)

SET (VSMC_CL_INCLUDE_DIR ${PROJECT_SOURCE_DIR}/include)
INCLUDE_DIRECTORIES (${PROJECT_SOURCE_DIR}/include)
SET (CMAKE_REQUIRED_INCLUDES ${CMAKE_REQUIRED_INCLUDES}
    ${PROJECT_SOURCE_DIR}/include)

IF (Boost_FOUND)
    INCLUDE_DIRECTORIES (${Boost_INCLUDE_DIRS})
    SET (CMAKE_REQUIRED_INCLUDES ${CMAKE_REQUIRED_INCLUDES}
        ${Boost_INCLUDE_DIRS})
ENDIF (Boost_FOUND)

IF (Eigen_FOUND)
    INCLUDE_DIRECTORIES (${Eigen_INCLUDE_DIR})
    SET (CMAKE_REQUIRED_INCLUDES ${CMAKE_REQUIRED_INCLUDES}
        ${Eigen_INCLUDE_DIR})
ENDIF (Eigen_FOUND)

IF (Random123_FOUND)
    INCLUDE_DIRECTORIES (${Random123_INCLUDE_DIR})
    SET (CMAKE_REQUIRED_INCLUDES ${CMAKE_REQUIRED_INCLUDES}
        ${Random123_INCLUDE_DIR})
ENDIF (Random123_FOUND)

IF (TBB_FOUND)
    INCLUDE_DIRECTORIES (${TBB_INCLUDE_DIR})
    SET (CMAKE_REQUIRED_INCLUDES ${CMAKE_REQUIRED_INCLUDES}
        ${TBB_INCLUDE_DIR})
ENDIF (TBB_FOUND)

IF (OPENCL_FOUND)
    INCLUDE_DIRECTORIES (${OPENCL_INCLUDE_DIR})
    SET (CMAKE_REQUIRED_INCLUDES ${CMAKE_REQUIRED_INCLUDES}
        ${OPENCL_INCLUDE_DIR})
ENDIF (OPENCL_FOUND)

SET (VSMC_TEST TRUE)

IF (Eigen_FOUND AND Random123_FOUND)
    # Test <chrono>
    FIND_PACKAGE (Boost COMPONENTS chrono system REQUIRED)
    IF (Boost_FOUND)
        SET (CHRONO_LINK_LIBRARIES
            ${Boost_CHRONO_LIBRARY_RELEASE}
            ${Boost_SYSTEM_LIBRARY_RELEASE})
        SET (CHRONO_LINK_LIBRARIES_DEBUG
            ${Boost_CHRONO_LIBRARY_DEBUG}
            ${Boost_SYSTEM_LIBRARY_DEBUG})
    ENDIF (Boost_FOUND)
    IF (UNIX AND (NOT APPLE) AND (NOT WIN32))
        FIND_LIBRARY (LINUX_RT rt)
        IF (LINUX_RT)
            SET (CHRONO_LINK_LIBRARIES ${CHRONO_LINK_LIBRARIES} ${LINUX_RT})
        ENDIF (LINUX_RT)
    ENDIF (UNIX AND (NOT APPLE) AND (NOT WIN32))
    SET (CMAKE_REQUIRED_LIBRARIES ${CHRONO_LINK_LIBRARIES_DEBUG})

    FILE (READ ${PROJECT_SOURCE_DIR}/cmake/try_chrono.cpp TRY_CHRONO_CPP)
    CHECK_CXX_SOURCE_RUNS ("${TRY_CHRONO_CPP}" VSMC_USE_STD_CHRONO)
    CHECK_CXX_SOURCE_RUNS ("${TRY_CHRONO_CPP}" VSMC_USE_BOOST_CHRONO)
    SET (CMAKE_REQUIRED_LIBRARIES)

    IF (VSMC_CHRONO)
        STRING (REGEX MATCH "std" VSMC_STD_CHRONO ${VSMC_CHRONO})
        STRING (REGEX MATCH "boost" VSMC_BOOST_CHRONO ${VSMC_CHRONO})
    ENDIF (VSMC_CHRONO)

    IF (NOT ( # No match between prefered and available
        (VSMC_STD_CHRONO AND VSMC_USE_STD_CHRONO) OR
        (VSMC_BOOST_CHRONO AND VSMC_USE_BOOST_CHRONO)))
        SET (VSMC_STD_CHRONO TRUE)
        SET (VSMC_BOOST_CHRONO TRUE)
    ENDIF (NOT ( # No match between prefered and available
        (VSMC_STD_CHRONO AND VSMC_USE_STD_CHRONO) OR
        (VSMC_BOOST_CHRONO AND VSMC_USE_BOOST_CHRONO)))

    IF (VSMC_STD_CHRONO AND VSMC_USE_STD_CHRONO)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_STD_CHRONO")
    ELSEIF (VSMC_BOOST_CHRONO AND VSMC_USE_BOOST_CHRONO)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_BOOST_CHRONO")
        SET (VSMC_TEST_LINK_LIBRARIES ${VSMC_TEST_LINK_LIBRARIES}
            ${CHRONO_LINK_LIBRARIES})
    ENDIF (VSMC_STD_CHRONO AND VSMC_USE_STD_CHRONO)

    # Test <functional>
    FILE (READ ${PROJECT_SOURCE_DIR}/cmake/try_function.cpp TRY_FUNCTION_CPP)
    CHECK_CXX_SOURCE_RUNS ("${TRY_FUNCTION_CPP}" VSMC_USE_STD_FUNCTION)
    CHECK_CXX_SOURCE_RUNS ("${TRY_FUNCTION_CPP}" VSMC_USE_BOOST_FUNCTION)

    IF (VSMC_FUNCTION)
        STRING (REGEX MATCH "std" VSMC_STD_FUNCTION ${VSMC_FUNCTION})
        STRING (REGEX MATCH "boost" VSMC_BOOST_FUNCTION ${VSMC_FUNCTION})
    ENDIF (VSMC_FUNCTION)

    IF (NOT ( # No match between prefered and available
        (VSMC_STD_FUNCTION AND VSMC_USE_STD_FUNCTION) OR
        (VSMC_BOOST_FUNCTION AND VSMC_USE_BOOST_FUNCTION)))
        SET (VSMC_STD_FUNCTION TRUE)
        SET (VSMC_BOOST_FUNCTION TRUE)
    ENDIF (NOT ( # No match between prefered and available
        (VSMC_STD_FUNCTION AND VSMC_USE_STD_FUNCTION) OR
        (VSMC_BOOST_FUNCTION AND VSMC_USE_BOOST_FUNCTION)))

    IF (VSMC_STD_FUNCTION AND VSMC_USE_STD_FUNCTION)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_STD_FUNCTION")
    ELSEIF (VSMC_BOOST_FUNCTION AND VSMC_USE_BOOST_FUNCTION)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_BOOST_FUNCTION")
    ELSE (VSMC_STD_FUNCTION AND VSMC_USE_STD_FUNCTION)
        SET (VSMC_TEST FALSE)
    ENDIF (VSMC_STD_FUNCTION AND VSMC_USE_STD_FUNCTION)

    # Test <random>
    FILE (READ ${PROJECT_SOURCE_DIR}/cmake/try_random.cpp TRY_RANDOM_CPP)
    CHECK_CXX_SOURCE_RUNS ("${TRY_RANDOM_CPP}" VSMC_USE_STD_RANDOM)
    CHECK_CXX_SOURCE_RUNS ("${TRY_RANDOM_CPP}" VSMC_USE_BOOST_RANDOM)

    IF (VSMC_RANDOM)
        STRING (REGEX MATCH "std" VSMC_STD_RANDOM ${VSMC_RANDOM})
        STRING (REGEX MATCH "boost" VSMC_BOOST_RANDOM ${VSMC_RANDOM})
    ENDIF (VSMC_RANDOM)

    IF (NOT ( # No match between prefered and available
        (VSMC_STD_RANDOM AND VSMC_USE_STD_RANDOM) OR
        (VSMC_BOOST_RANDOM AND VSMC_USE_BOOST_RANDOM)))
        SET (VSMC_STD_RANDOM TRUE)
        SET (VSMC_BOOST_RANDOM TRUE)
    ENDIF (NOT ( # No match between prefered and available
        (VSMC_STD_RANDOM AND VSMC_USE_STD_RANDOM) OR
        (VSMC_BOOST_RANDOM AND VSMC_USE_BOOST_RANDOM)))

    IF (VSMC_STD_RANDOM AND VSMC_USE_STD_RANDOM)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_STD_RANDOM")
    ELSEIF (VSMC_BOOST_RANDOM AND VSMC_USE_BOOST_RANDOM)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_BOOST_RANDOM")
    ELSE (VSMC_STD_RANDOM AND VSMC_USE_STD_RANDOM)
        SET (VSMC_TEST FALSE)
    ENDIF (VSMC_STD_RANDOM AND VSMC_USE_STD_RANDOM)

    # Test <type_traits>
    FILE (READ ${PROJECT_SOURCE_DIR}/cmake/try_type_traits.cpp
        TRY_TYPE_TRAITS_CPP)
    CHECK_CXX_SOURCE_RUNS ("${TRY_TYPE_TRAITS_CPP}"
        VSMC_USE_STD_TYPE_TRAITS)
    CHECK_CXX_SOURCE_RUNS ("${TRY_TYPE_TRAITS_CPP}"
        VSMC_USE_BOOST_TYPE_TRAITS)

    IF (VSMC_TYPE_TRAITS)
        STRING (REGEX MATCH "std" VSMC_STD_TYPE_TRAITS
            ${VSMC_TYPE_TRAITS})
        STRING (REGEX MATCH "boost" VSMC_BOOST_TYPE_TRAITS
            ${VSMC_TYPE_TRAITS})
    ENDIF (VSMC_TYPE_TRAITS)

    IF (NOT ( # No match between prefered and available
        (VSMC_STD_TYPE_TRAITS AND VSMC_USE_STD_TYPE_TRAITS) OR
        (VSMC_BOOST_TYPE_TRAITS AND VSMC_USE_BOOST_TYPE_TRAITS)))
        SET (VSMC_STD_TYPE_TRAITS TRUE)
        SET (VSMC_BOOST_TYPE_TRAITS TRUE)
    ENDIF (NOT ( # No match between prefered and available
        (VSMC_STD_TYPE_TRAITS AND VSMC_USE_STD_TYPE_TRAITS) OR
        (VSMC_BOOST_TYPE_TRAITS AND VSMC_USE_BOOST_TYPE_TRAITS)))

    IF (VSMC_STD_TYPE_TRAITS AND VSMC_USE_STD_TYPE_TRAITS)
        SET (VSMC_TEST_FLAGS
            "${VSMC_TEST_FLAGS} -DVSMC_USE_STD_TYPE_TRAITS")
    ELSEIF (VSMC_BOOST_TYPE_TRAITS AND VSMC_USE_BOOST_TYPE_TRAITS)
        SET (VSMC_TEST_FLAGS
            "${VSMC_TEST_FLAGS} -DVSMC_USE_BOOST_TYPE_TRAITS")
    ELSE (VSMC_STD_TYPE_TRAITS AND VSMC_USE_STD_TYPE_TRAITS)
        SET (VSMC_TEST FALSE)
    ENDIF (VSMC_STD_TYPE_TRAITS AND VSMC_USE_STD_TYPE_TRAITS)

    # Test lgamma
    FILE (READ ${PROJECT_SOURCE_DIR}/cmake/try_lgamma.cpp TRY_LGAMMA_CPP)
    CHECK_CXX_SOURCE_RUNS ("${TRY_LGAMMA_CPP}" VSMC_USE_C99_LGAMMA)
    CHECK_CXX_SOURCE_RUNS ("${TRY_LGAMMA_CPP}" VSMC_USE_STD_LGAMMA)
    CHECK_CXX_SOURCE_RUNS ("${TRY_LGAMMA_CPP}" VSMC_USE_BOOST_LGAMMA)

    SET (VSMC_HAS_LGAMMA TRUE)
    IF (VSMC_USE_C99_LGAMMA)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_C99_LGAMMA")
    ELSEIF (VSMC_USE_STD_LGAMMA)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_STD_LGAMMA")
    ELSEIF (VSMC_USE_BOOST_LGAMMA)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_BOOST_LGAMMA")
    ELSE (VSMC_USE_C99_LGAMMA)
        SET (VSMC_HAS_LGAMMA FALSE)
    ENDIF (VSMC_USE_C99_LGAMMA)

    # Test erf
    FILE (READ ${PROJECT_SOURCE_DIR}/cmake/try_erf.cpp TRY_ERF_CPP)
    CHECK_CXX_SOURCE_RUNS ("${TRY_ERF_CPP}" VSMC_USE_C99_ERF)
    CHECK_CXX_SOURCE_RUNS ("${TRY_ERF_CPP}" VSMC_USE_STD_ERF)
    CHECK_CXX_SOURCE_RUNS ("${TRY_ERF_CPP}" VSMC_USE_BOOST_ERF)

    SET (VSMC_HAS_ERF TRUE)
    IF (VSMC_USE_C99_ERF)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_C99_ERF")
    ELSEIF (VSMC_USE_STD_ERF)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_STD_ERF")
    ELSEIF (VSMC_USE_BOOST_ERF)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_BOOST_ERF")
    ELSE (VSMC_USE_C99_ERF)
        SET (VSMC_HAS_ERF FALSE)
    ENDIF (VSMC_USE_C99_ERF)

    # Test isinf
    FILE (READ ${PROJECT_SOURCE_DIR}/cmake/try_isinf.cpp TRY_ISINF_CPP)
    CHECK_CXX_SOURCE_RUNS ("${TRY_ISINF_CPP}" VSMC_USE_C99_ISINF)
    CHECK_CXX_SOURCE_RUNS ("${TRY_ISINF_CPP}" VSMC_USE_STD_ISINF)
    CHECK_CXX_SOURCE_RUNS ("${TRY_ISINF_CPP}" VSMC_USE_BOOST_ISINF)

    SET (VSMC_HAS_ISINF TRUE)
    IF (VSMC_USE_C99_ISINF)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_C99_ISINF")
    ELSEIF (VSMC_USE_STD_ISINF)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_STD_ISINF")
    ELSEIF (VSMC_USE_BOOST_ISINF)
        SET (VSMC_TEST_FLAGS "${VSMC_TEST_FLAGS} -DVSMC_USE_BOOST_ISINF")
    ELSE (VSMC_USE_C99_ISINF)
        SET (VSMC_HAS_ISINF FALSE)
    ENDIF (VSMC_USE_C99_ISINF)
ELSE (Eigen_FOUND AND Random123_FOUND)
    SET (VSMC_TEST FALSE)
ENDIF (Eigen_FOUND AND Random123_FOUND)

IF (VSMC_TEST)
    MESSAGE (STATUS "Build tests")
    ENABLE_TESTING ()
    ADD_SUBDIRECTORY (test EXCLUDE_FROM_ALL)
ENDIF (VSMC_TEST)
