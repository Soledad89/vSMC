# ============================================================================
#  vSMC/cmake/vSMCFindJEMALLOC.cmake
# ----------------------------------------------------------------------------
#                          vSMC: Scalable Monte Carlo
# ----------------------------------------------------------------------------
#  Copyright (c) 2013-2015, Yan Zhou
#  All rights reserved.
#
#  Redistribution and use in source and binary forms, with or without
#  modification, are permitted provided that the following conditions are met:
#
#    Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
#
#    Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
#  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS AS IS
#  AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
#  IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
#  ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
#  LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
#  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
#  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
#  INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
#  CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
#  ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
#  POSSIBILITY OF SUCH DAMAGE.
# ============================================================================

IF (DEFINED VSMC_JEMALLOC_FOUND)
    RETURN ()
ENDIF (DEFINED VSMC_JEMALLOC_FOUND)

FILE (READ ${CMAKE_CURRENT_LIST_DIR}/vSMCFindJEMALLOC.cpp
    VSMC_JEMALLOC_TEST_SOURCE)

IF (NOT DEFINED JEMALLOC_FOUND)
    INCLUDE (FindJEMALLOC)
ENDIF (NOT DEFINED JEMALLOC_FOUND)
IF (NOT JEMALLOC_FOUND)
    RETURN ()
ENDIF (NOT JEMALLOC_FOUND)

INCLUDE (CheckCXXSourceRuns)
SET (SAFE_CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS})
SET (SAFE_CMAKE_REQUIRED_INCLUDES  ${CMAKE_REQUIRED_INCLUDES})
SET (SAFE_CMAKE_REQUIRED_LIBRARIES ${CMAKE_REQUIRED_LIBRARIES})
SET (CMAKE_REQUIRED_INCLUDES ${SAFE_CMAKE_REQUIRED_INCLUDES}
    ${JEMALLOC_INCLUDE_DIR})
SET (CMAKE_REQUIRED_LIBRARIES ${SAFE_CMAKE_REQUIRED_LIBRARIES}
    ${JEMALLOC_LINK_LIBRARIES})
SET (CMAKE_REQUIRED_DEFINITIONS ${SAFE_CMAKE_REQUIRED_DEFINITIONS}
    -DVSMC_HAS_JEMALLOC_STDAPI=1)
CHECK_CXX_SOURCE_RUNS ("${VSMC_JEMALLOC_TEST_SOURCE}"
    VSMC_JEMALLOC_STDAPI_FOUND)
IF (NOT VSMC_JEMALLOC_STDAPI_FOUND)
    SET (CMAKE_REQUIRED_DEFINITIONS ${SAFE_CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_JEMALLOC_STDAPI=0)
    CHECK_CXX_SOURCE_RUNS ("${VSMC_JEMALLOC_TEST_SOURCE}"
        VSMC_JEMALLOC_JEAPI_FOUND)
ENDIF (NOT VSMC_JEMALLOC_STDAPI_FOUND)
IF (VSMC_JEMALLOC_STDAPI_FOUND OR VSMC_JEMALLOC_JEAPI_FOUND)
    SET (VSMC_JEMALLOC_FOUND TRUE CACHE BOOL "Found jemalloc")
    IF (VSMC_JEMALLOC_STDAPI_FOUND)
        MESSAGE (STATUS "Found jemalloc standard API")
        SET (VSMC_JEMALLOC_DEFINITIONS -DVSMC_HAS_JEMALLOC_STDAPI=1
            CACHE STRING "jemalloc compile time definitions")
    ELSE (VSMC_JEMALLOC_STDAPI_FOUND)
        MESSAGE (STATUS "Found jemalloc mangled API")
        SET (VSMC_JEMALLOC_DEFINITIONS -DVSMC_HAS_JEMALLOC_STDAPI=0
            CACHE STRING "jemalloc compile time definitions")
    ENDIF (VSMC_JEMALLOC_STDAPI_FOUND)
ELSE (VSMC_JEMALLOC_STDAPI_FOUND OR VSMC_JEMALLOC_JEAPI_FOUND)
    SET (VSMC_JEMALLOC_FOUND FALSE CACHE BOOL "Found jemalloc")
ENDIF (VSMC_JEMALLOC_STDAPI_FOUND OR VSMC_JEMALLOC_JEAPI_FOUND)
SET (CMAKE_REQUIRED_DEFINITIONS ${SAFE_CMAKE_REQUIRED_DEFINITIONS})
SET (CMAKE_REQUIRED_INCLUDES ${SAFE_CMAKE_REQUIRED_INCLUDES})
SET (CMAKE_REQUIRED_LIBRARIES ${SAFE_CMAKE_REQUIRED_LIBRARIES})
